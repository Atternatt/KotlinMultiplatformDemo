// required te buildscript to add some of the plugins:w
buildscript {
  repositories {
    google()
    jcenter()
    mavenCentral()
  }

  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.4.0"
    classpath "com.squareup.sqldelight:gradle-plugin:1.4.0"
    classpath "org.jetbrains.kotlin:kotlin-serialization:1.4.0"
  }
}

plugins {
  id("com.android.library")
  id 'org.jetbrains.kotlin.multiplatform'
  id 'kotlin-kapt'
  id 'kotlin-android-extensions'
  id 'kotlinx-serialization'
}

apply plugin: "com.squareup.sqldelight"

group 'com.harmony.core'
version '0.0.1'

// configuration of the Android preset
android {
  defaultConfig {
    minSdkVersion 16
    compileSdkVersion 28
  }
  packagingOptions {
    exclude "META-INF/*"
  }
}

kotlin {
  final def iosTarget = System.getenv('SDK_NAME')?.startsWith("iphoneos") ? presets.iosArm64 : presets.iosX64
  targetFromPreset(iosTarget, "ios") {
    binaries {
      framework("SDK")
    }
  }

  android()

  jvm()

  sourceSets {
    commonMain {
      dependencies {
        implementation kotlin('stdlib-common')

        implementation("org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.8")
        implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-common:1.3.8"

        // serialization library
        implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime-common:1.0.0-RC"

        // SQL Delight
        implementation "com.squareup.sqldelight:runtime:1.4.0"

        // http client library
        implementation "io.ktor:ktor-client-core:1.3.2"
        implementation "io.ktor:ktor-client-json:1.3.2"
        implementation "io.ktor:ktor-client-logging:1.3.2"
        implementation "io.ktor:ktor-client-serialization:1.3.2"

        // date library
        implementation "com.soywiz.korlibs.klock:klock:1.8.0"
      }
    }

    commonTest {
      dependencies {

        implementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.3.8"

        implementation kotlin('test-common')
        implementation kotlin('test-annotations-common')
      }
    }

    jvmMain {
      dependencies {
        implementation kotlin('stdlib-jdk8')
        api "com.google.code.gson:gson:2.8.6"
        implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime:1.0.0-RC"
        implementation "com.squareup.sqldelight:sqlite-driver:1.4.0"
      }
    }

    jvmTest {
      dependencies {
        implementation kotlin('test')
        implementation kotlin('test-junit')
      }
    }

    androidMain {
      dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib:1.4.0"

        implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.8"

        //arch components
        api "android.arch.lifecycle:runtime:1.1.1"
        api "android.arch.lifecycle:extensions:1.1.1"
        implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'


        // Database
        implementation "androidx.sqlite:sqlite:2.1.0"
        implementation "androidx.sqlite:sqlite-framework:2.1.0"

        // SQL Delight
        implementation "com.squareup.sqldelight:android-driver:1.4.0"

        // serialization
        implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime:1.0.0-RC"

        // ktor android
        implementation "io.ktor:ktor-client-android:1.3.2"
        implementation "io.ktor:ktor-client-core-jvm:1.3.2"
        implementation "io.ktor:ktor-client-json-jvm:1.3.2"
        implementation "io.ktor:ktor-client-logging-jvm:1.3.2"
        implementation "io.ktor:ktor-client-serialization-jvm:1.3.2"
      }
    }
    androidTest {
      dependencies {
        implementation "org.jetbrains.kotlin:kotlin-test-junit:1.4.0"

        implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.8"
        implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.8"

        implementation "io.ktor:ktor-client-mock-jvm:1.3.2"
      }
    }
    iosMain {
      dependencies {
        implementation("org.jetbrains.kotlin:kotlin-stdlib")
        implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-native:1.3.8"

        implementation "com.squareup.sqldelight:native-driver:1.4.0"
        implementation("org.jetbrains.kotlinx:kotlinx-serialization-runtime-native:1.0.0-RC")

        // ktor ios runtine
        implementation("io.ktor:ktor-client-ios:1.3.2")
        implementation("io.ktor:ktor-client-json-native:1.3.2")
        implementation("io.ktor:ktor-client-serialization-native:1.3.2")
        implementation("io.ktor:ktor-client-logging-native:1.3.2")
        implementation("org.jetbrains.kotlinx:kotlinx-serialization-runtime-native:1.0.0-RC")

      }

    }
    iosTest {

    }
  }
}

dependencies {
  kapt "android.arch.lifecycle:compiler:1.1.1"
}

sqldelight {
  CacheDatabase {
    packageName = "com.harmony.kotlin.data.datasource.database"
    sourceFolders = ["sqldelight"]
  }
}

configurations {
  compileClasspath
}

task copyFramework {
  final File frameworkDir = new File(buildDir, "xcode-frameworks")
  def buildType = project.findProperty('kotlin.build.type') ?: 'DEBUG'
  def framework = kotlin.targets.ios.compilations.main.target.binaries.findFramework("Shared", buildType)

  doLast {
    def srcFile = framework.outputFile
    copy {
      from srcFile.parent
      into frameworkDir
      include 'Shared.framework/**'
      include 'Shared.framework.dSYM'
    }
  }
}
